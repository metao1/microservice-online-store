pluginManagement {
  repositories {
    mavenLocal()
    gradlePluginPortal()
    mavenCentral()
  }
}

rootProject.name = 'microservice-online-store'

def includeIfDirExists = { String projectPath, String dirPath ->
  File projectDir = file(dirPath)
  if (projectDir.exists() && projectDir.isDirectory()) {
    include(projectPath)
    project(projectPath).projectDir = projectDir
    return true
  }
  return false
}

def springBootProjects = []
if (includeIfDirExists(':inventory-microservice', 'inventory-microservice')) {
  springBootProjects << ':inventory-microservice'
}
if (includeIfDirExists(':payment-microservice', 'payment-microservice')) {
  springBootProjects << ':payment-microservice'
}
if (includeIfDirExists(':order-microservice', 'order-microservice')) {
  springBootProjects << ':order-microservice'
}

includeIfDirExists(':shared-kernel', 'shared-kernel')
includeIfDirExists(':shared-test', 'shared-test')
includeIfDirExists(':kafka-lib', 'kafka-lib')
includeIfDirExists(':performance-loadtest', 'performance-loadtest')

gradle.ext.springBootProjects = springBootProjects
