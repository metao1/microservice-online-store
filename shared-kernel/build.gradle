plugins {
  id 'java-library'
  id 'maven-publish'
  id "com.google.protobuf" version "0.9.4"
}

ext {
  springBootDependenciesVersion = "4.0.2"
}

def commitRefName = System.getenv("CI_COMMIT_REF_NAME") ?: "1.0"

version = commitRefName
group = "com.metao.book.shared"

dependencies {
  // Spring
  compileOnly "org.springframework.boot:spring-boot-starter-validation"
  compileOnly "org.springframework.boot:spring-boot-starter-data-jpa"
  testImplementation "org.springframework.boot:spring-boot-starter-validation"
  testImplementation "org.springframework.boot:spring-boot-starter-data-jpa"

  // Jackson - needed for JSON annotations
  compileOnly "org.springframework.boot:spring-boot-jackson"
  compileOnly "com.fasterxml.jackson.core:jackson-databind"
  compileOnly "com.fasterxml.jackson.core:jackson-annotations"
  testImplementation "com.fasterxml.jackson.core:jackson-databind"
  testImplementation "com.fasterxml.jackson.core:jackson-annotations"

  // Micrometer (version via BOM to stay aligned with Spring Boot)
  compileOnly "io.micrometer:micrometer-core"
  compileOnly "io.micrometer:micrometer-registry-prometheus"
  testImplementation "io.micrometer:micrometer-core"
  testImplementation "io.micrometer:micrometer-registry-prometheus"

  // Protobuf runtime needed by generated domain events
  api "com.google.protobuf:protobuf-java:3.25.5"

}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId = 'com.metao.book.shared'
      artifactId = 'shared-kernel'
      version = commitRefName
    }
  }
  repositories {
    maven {
      name = "local"
      url = uri("file://${System.getProperty('user.home')}/.m2/repository")
    }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'src/main/java', 'build/generated/source/proto/java/main/java'
    }
  }
}

protobuf {
  protoc {
    if (osdetector.os == "osx") {
      artifact = 'com.google.protobuf:protoc:3.25.5:osx-x86_64'
    } else {
      artifact = 'com.google.protobuf:protoc:3.25.5'
    }
  }

  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.70.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootDependenciesVersion}"
  }
}

def getCurrentGitBranch() {
  def gitBranch = "unknown-branch"
  try {
    def workingDir = new File("${project.projectDir}")
    def result = "git rev-parse --abbrev-ref HEAD".execute(null, workingDir)
    result.waitFor()
    if (result.exitValue() == 0) {
      gitBranch = result.text.trim()
    }
  } catch (e) {
    logger.error("Unable to fetch current GIT branch", e)
  }
  logger.info("Your current GIT branch is {}", gitBranch)
  return gitBranch
}
