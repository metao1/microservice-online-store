dependencies {
  implementation project(':shared-kernel')
  implementation project(':kafka-lib')
  // Make shared proto files available to protoc
  protobuf project(':shared-kernel')

  implementation "org.springframework.boot:spring-boot-starter-data-jpa"
  implementation "org.springframework.boot:spring-boot-starter-validation"

  implementation "org.springframework.kafka:spring-kafka"
  runtimeOnly "org.postgresql:postgresql:42.7.3"

  implementation "io.confluent:kafka-protobuf-serializer:7.8.0"

  implementation "org.flywaydb:flyway-database-postgresql:10.11.0"
  implementation "org.springframework.boot:spring-boot-flyway"
  implementation "org.springframework.boot:spring-boot-starter-actuator"

  implementation "org.flywaydb:flyway-core"

  testImplementation "io.rest-assured:rest-assured:5.4.0"
  testImplementation "org.apache.groovy:groovy:4.0.16"
  testImplementation "org.apache.groovy:groovy-json:4.0.16"
  testImplementation "org.apache.groovy:groovy-xml:4.0.16"
  testImplementation 'org.mockito:mockito-core:5.7.0'
  if (rootProject.findProject(':shared-test') != null) {
    testImplementation project(':shared-test')
  }
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.kafka:spring-kafka-test'
  testImplementation "org.testcontainers:testcontainers-postgresql:${testcontainersVersion}"

  testImplementation 'org.wiremock.integrations:wiremock-spring-boot:4.1.0'
}

sourceSets {
  main {
    java {
      srcDirs 'src/main/java', 'build/generated/source/proto/java/main/java'
    }
  }
}

protobuf {
  protoc {
    if (osdetector.os == "osx") {
      artifact = 'com.google.protobuf:protoc:3.25.5:osx-x86_64'
    } else {
      artifact = 'com.google.protobuf:protoc:3.25.5'
    }
  }

  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.70.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

bootJar {
  layered { enabled = true }
}

// Drop unused compression native dependency (~7MB) from Kafka clients
configurations.configureEach {
  exclude group: "com.github.luben", module: "zstd-jni"
}

tasks.named('build') {
  dependsOn 'generateProto'
}
