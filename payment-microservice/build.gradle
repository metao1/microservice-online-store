dependencies {
  implementation project(':shared-kernel')
  implementation project(':kafka-lib')
  // Make shared proto files available to protoc
  protobuf project(':shared-kernel')

  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation "org.springframework.boot:spring-boot-starter-data-jpa"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.kafka:spring-kafka"

  runtimeOnly "org.postgresql:postgresql:42.7.3"

  implementation "io.confluent:kafka-protobuf-serializer:7.8.0"

  implementation "org.flywaydb:flyway-database-postgresql:10.11.0"
  implementation "org.springframework.boot:spring-boot-starter-actuator"

  implementation "org.flywaydb:flyway-core"

  // Hibernate second-level cache
  implementation "org.hibernate.orm:hibernate-jcache"
  implementation "com.github.ben-manes.caffeine:caffeine:3.1.8"
  implementation "com.github.ben-manes.caffeine:jcache:3.1.8"

  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation "org.springframework.kafka:spring-kafka-test"
  testImplementation project(':shared-test')
  testImplementation "org.testcontainers:testcontainers-postgresql:${testcontainersVersion}"
  testImplementation "io.rest-assured:rest-assured:5.4.0"
  testImplementation "org.mockito:mockito-core:4.8.0"
  testImplementation "org.testcontainers:testcontainers-jdbc:${testcontainersVersion}"

  testRuntimeOnly 'org.postgresql:postgresql'
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java', 'build/generated/source/proto/java/main/java']
    }
  }
}

protobuf {
  protoc {
    if (osdetector.os == "osx") {
      artifact = 'com.google.protobuf:protoc:3.25.5:osx-x86_64'
    } else {
      artifact = 'com.google.protobuf:protoc:3.25.5'
    }
  }

  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.70.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

tasks.named('build') {
  dependsOn 'generateProto'
}

bootJar {
  layered { enabled = true }
}

// Drop unused compression native dependency (~7MB) from Kafka clients
configurations.configureEach {
  exclude group: "com.github.luben", module: "zstd-jni"
}
