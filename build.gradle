plugins {
  id "base"
  id "idea"
  id "org.flywaydb.flyway" version "10.11.0" apply false
  id "com.google.cloud.tools.jib" version "3.1.4" apply false
  id "org.springframework.boot" version "4.0.2" apply false
  id "io.spring.dependency-management" version "1.1.7" apply false
  id "com.google.protobuf" version "0.9.4" apply false
}

ext {
  micrometerVersion = "1.16.2"
  testcontainersVersion = "2.0.2"
  springBootDependenciesVersion = "4.0.2"
  resilience4jVersion = "2.2.0"
}

subprojects {
  group "com.metao.book"
  apply plugin: "java"
  apply plugin: "com.google.protobuf"
  apply plugin: "jacoco"
  apply plugin: "io.spring.dependency-management"

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://packages.confluent.io/maven/" }
  }
  List<String> springBootProjects = (gradle.ext.has('springBootProjects') ? gradle.springBootProjects : []) as List<String>
  boolean isSpringProject = springBootProjects.contains(it.path)
  if (isSpringProject) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.flywaydb.flyway"
    dependencies {
      implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
      }
      implementation "org.springframework.boot:spring-boot-autoconfigure"
      implementation "commons-codec:commons-codec:1.17.1"
      implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"

      testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
    dependencyManagement {
      imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootDependenciesVersion}"
      }
    }

    bootJar {
      exclude("data/**")
    }
  }

  dependencies {

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation "org.awaitility:awaitility:4.3.0"
    testImplementation "org.mockito:mockito-core:4.8.0"

    // Lombok
    implementation "org.projectlombok:lombok:1.18.36"
    annotationProcessor "org.projectlombok:lombok:1.18.36"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.36"

    testImplementation "org.testcontainers:testcontainers-junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:testcontainers-postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  }

  configurations.configureEach {
    test {
      useJUnitPlatform()
      environment "TESTCONTAINERS_DOCKER_SOCKET_OVERRIDE", "/var/run/docker.sock"
      doFirst {
        systemProperty "spring.profiles.active", "test"
      }

      testLogging {
        events "PASSED", "SKIPPED", "FAILED"
      }
    }

    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    exclude group: "org.slf4j", module: "slf4j-simple"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.junit.vintage:junit-vintage-engine"
    dependencyManagement {
      imports {
        mavenBom "io.micrometer:micrometer-bom:${micrometerVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootDependenciesVersion}"
      }
    }
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  // Parallelize test forks to overlap I/O-heavy Testcontainers work
  tasks.withType(Test).configureEach {
    maxParallelForks = Math.max(1, Runtime.runtime.availableProcessors().intdiv(2))
  }

  jacocoTestReport {
    dependsOn test
    reports {
      xml.required = true
      html.required = true
    }
  }
}

tasks.register('buildDependencies') {
  [':shared-kernel', ':kafka-lib', ':shared-test',
   ':inventory-microservice', ':payment-microservice', ':order-microservice'].each { modulePath ->
    if (findProject(modulePath) != null) {
      dependsOn "${modulePath}:build"
    }
  }
}

tasks.register('publishLocalDependencies') {
  [':shared-kernel', ':kafka-lib', ':shared-test'].each { modulePath ->
    if (findProject(modulePath) != null) {
      dependsOn "${modulePath}:publishToMavenLocal"
    }
  }
}

tasks.named('build') {
  dependsOn ':buildDependencies'
}
